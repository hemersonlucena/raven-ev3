'DECLARAÇÃO DE VARIÁVEIS
Mapa = "@@ABCDEFGHIJKLMNOPQRSTUVWXYZ .?!"
Mensagem = "LEGO!"
Decimal = 0
Bit[0] = 0


'SUBROTINAS
Sub RESETARATUADOR
  Motor.StartPower("A", 25)
  InicioReset = EV3.Time
  While EV3.Time < InicioReset + 3000
  EndWhile
  Motor.MovePower("A", -20, 30, "True")
EndSub


Sub RESETARBITS
  For j = 0 To 4
    Bit[j] = 0
  EndFor
EndSub


Sub CONVERTERPARABINARIO
  RESETARBITS()
  Aux = Decimal
  k = 4
  While Aux > 0 And k >= 0
    AuxInteiro = Math.Floor(Aux/2)
    Bit[k] = Aux - (AuxInteiro*2)
    Aux = AuxInteiro
    k = k - 1
  EndWhile
EndSub


Sub ENVIARBITS
  For l = 0 To 4
    If Bit[l] = 1 Then
      PULSAR()
      PULSAR()
    Else
      PULSAR()
    EndIf
      Program.Delay(500)
  EndFor
EndSub


Sub ENVIARMENSAGEM
  For i = 1 To Text.GetLength(Mensagem)
    Decimal = Text.GetIndexOf(Mapa, Text.GetSubText(Mensagem, i, 1))
    Decimal = Decimal - 1
    CONVERTERPARABINARIO()
    ENVIARBITS()
  EndFor
EndSub


Sub PULSAR
  Motor.Move("A", 40, 28, "True")
  Motor.Move("A", -40, 28, "True")
EndSub


'INÍCIO
RESETARATUADOR()
Program.Delay(3000)
ENVIARMENSAGEM()